apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'war'
apply plugin: 'appengine'

buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.google.appengine:gradle-appengine-plugin:1.9.15'
    }
}

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = '1.0.0'


jar {
    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': version
    }
}

repositories {
    mavenCentral()
}

dependencies {
    appengineSdk 'com.google.appengine:appengine-java-sdk:1.9.15'

    
    providedCompile "javax.servlet:servlet-api:2.5"
    

    //compile group: 'commons-collections', name: 'commons-collections', version: '3.2'
    testCompile group: 'junit', name: 'junit', version: '4.+'
}

test {
    systemProperties 'property': 'value'
}

appengine {
    downloadSdk = true

    httpPort = 8085

    appcfg {
        email = 'edson.schlei@gmail.com'
        passIn = true

        logs {
            severity = 1
            outputFile = file('mylogs.txt')
        }

        app {
            id = 'ees-test1'
        }
    }
}

sourceSets {
    main.resources.srcDirs += 'src/main/webapp'
}

eclipse {
    project {
        name = 'RestNinjaGAE'
        file {
            whenMerged { project ->
                project.natures << 'org.springsource.ide.eclipse.gradle.core.nature'
                project.natures << 'com.google.appengine.eclipse.core.gaeNature'
                project.natures << 'com.google.gdt.eclipse.core.webAppNature'
                project.natures << 'org.eclipse.wst.common.project.facet.core.nature'
                
                project.buildCommands << [name: 'org.eclipse.wst.common.project.facet.core.builder']
                project.buildCommands << [name: 'com.google.gdt.eclipse.core.webAppProjectValidator']
                project.buildCommands << [name: 'com.google.appengine.eclipse.core.enhancerbuilder']
                project.buildCommands << [name: 'com.google.appengine.eclipse.core.projectValidator']
                project.buildCommands << [name: 'com.google.appengine.eclipse.core.gaeProjectChangeNotifier']
            }
        }
    }
    classpath {
        containers.removeAll { it.startsWith('org.eclipse.jdt.launching.JRE_CONTAINER') }
        containers 'org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-1.7'
        containers 'com.google.appengine.eclipse.core.GAE_CONTAINER'
    }
}


eclipseProject << {
    def props = new Properties()
    file(".settings/com.google.gdt.eclipse.core.prefs").withInputStream {
        stream -> props.load(stream)
    }
    props.setProperty("warSrcDir", "src/main/webapp")
    props.setProperty("warSrcDirIsOutput", "false")
    file(".settings/com.google.gdt.eclipse.core.prefs").withOutputStream {
        stream -> props.store(stream, null)
    }
}
